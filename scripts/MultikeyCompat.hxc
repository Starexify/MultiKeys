import funkin.data.song.importer.FNFLegacyImporter;
import funkin.graphics.FunkinSprite;
import funkin.modding.module.ScriptedModule;
import funkin.modding.events.ScriptEvent;
import funkin.play.notes.Strumline;
import funkin.ui.debug.charting.ChartEditorState;
import funkin.ui.debug.charting.components.ChartEditorEventSprite;
import funkin.ui.debug.charting.handlers.ChartEditorThemeHandler;
import funkin.ui.debug.charting.handlers.ChartEditorToolboxHandler;
import funkin.util.FileWriteMode;

import flixel.FlxG;

import haxe.ui.components.Label;
import haxe.ui.components.NumberStepper;
import haxe.ui.components.VerticalRule;
import haxe.ui.containers.Box;
import haxe.ui.containers.Frame;
import haxe.ui.containers.Grid;
import haxe.ui.containers.HBox;
import haxe.ui.events.UIEvent;

class MultikeyCompat extends ScriptedModule {
    var chartEditor:ChartEditorState;

    var DEFAULT_KEY_COUNT:Int = 4;

    var addedComponent:Bool;
    var addedKeysComponent:Bool;
    var box:Null<Box>;

    function new() {
        super("MultikeyCompat");
    }

    override function onStateChangeEnd(event:StateChangeScriptEvent) {
        if (Std.isOfType(event.targetState, ChartEditorState)) {
            chartEditor = event.targetState;
            updateChartEditor(chartEditor, DEFAULT_KEY_COUNT);
        }
    }

    override function onUpdate(event:UpdateScriptEvent) {
        if (chartEditor != null) {
            //var keyCount = FileUtil.readJSONFromPath("assets/data/songs/2hot/2hot-metadata.json")?.keysCount;
            //trace(chartEditor.get_currentSongId());

            // Add Key Count component to the Song Metadata toolbox
            if (!addedComponent && chartEditor.activeToolboxes.exists(ChartEditorState.CHART_EDITOR_TOOLBOX_METADATA_LAYOUT)) {
                var metadataToolbox = chartEditor.activeToolboxes.get(ChartEditorState.CHART_EDITOR_TOOLBOX_METADATA_LAYOUT);
                var frameVariation = metadataToolbox.findComponent("frameVariation", Frame, true);

                if (frameVariation != null) {
                    var grid = frameVariation.findComponent(null, Grid, true);
                    if (grid != null) {
                        var keyCountLabel:Label = new Label();
                        keyCountLabel.text = "Key Count:";
                        keyCountLabel.verticalAlign = "center";
                        keyCountLabel.horizontalAlign = "right";

                        var keyCount:NumberStepper = new NumberStepper();
                        keyCount.id = "keyCount";
                        keyCount.pos = ChartEditorState.STRUMLINE_SIZE;
                        keyCount.min = 1;
                        keyCount.max = 9;
                        keyCount.tooltip = "Set the key count of the song variation.";
                        keyCount.onChange = (event:UIEvent) -> updateChartEditor(chartEditor, event.target.pos);

                        grid.addComponent(keyCountLabel);
                        grid.addComponent(keyCount);

                        metadataToolbox.height += 30;
                        frameVariation.height += 30;
                        addedComponent = true;
                    }
                }
            }

            // Add Keys to the Playbar
            if (box == null) box = chartEditor.root.findComponent("playbar", Box, false);
            if (!addedKeysComponent && box != null) {
                var hbox = box.findComponents(null, HBox, 1)[2];
                if (hbox != null) {
                    var remainingLabel = hbox.findComponent("playbarSongRemaining", Label, false);
                    if (remainingLabel != null) hbox.removeComponent(remainingLabel);

                    var rule = new VerticalRule();
                    rule.percentHeight = 80;

                    var playbarKeys = new Label();
                    playbarKeys.id = "playbarKeys";
                    playbarKeys.styleNames = "playbarStatus";
                    playbarKeys.verticalAlign = "center";
                    playbarKeys.tooltip = "Left Click to Increase Key Count\nRight Click to Decrease Key Count\nCtrl+Click to open Metadata Window";
                    function updateKeysDisplay() playbarKeys.text = "Keys: " + ChartEditorState.STRUMLINE_SIZE;
                    updateKeysDisplay();

                    playbarKeys.onClick = (event:UIEvent) -> {
                        if (chartEditor.pressingControl()) ChartEditorToolboxHandler.setToolboxState(chartEditor, ChartEditorState.CHART_EDITOR_TOOLBOX_METADATA_LAYOUT, true);
                        else { if (ChartEditorState.STRUMLINE_SIZE < 9) {
                            ChartEditorToolboxHandler.refreshToolbox(chartEditor, ChartEditorState.CHART_EDITOR_TOOLBOX_METADATA_LAYOUT);
                            ChartEditorState.STRUMLINE_SIZE++;
                            updateChartEditor(chartEditor, ChartEditorState.STRUMLINE_SIZE);
                            updateKeysDisplay();
                        }}
                    }
                    playbarKeys.onRightClick = (event:UIEvent) -> if (ChartEditorState.STRUMLINE_SIZE > 1) {
                        ChartEditorToolboxHandler.refreshToolbox(chartEditor, ChartEditorState.CHART_EDITOR_TOOLBOX_METADATA_LAYOUT);
                        ChartEditorState.STRUMLINE_SIZE--;
                        updateChartEditor(chartEditor, ChartEditorState.STRUMLINE_SIZE);
                        updateKeysDisplay();
                    }

                    var newRemainingLabel = new Label();
                    newRemainingLabel.id = "playbarSongRemaining";
                    newRemainingLabel.text = "-1:00";
                    newRemainingLabel.styleNames = "playbarDuration";

                    hbox.addComponent(playbarKeys);
                    hbox.addComponent(rule);
                    hbox.addComponent(newRemainingLabel);
                    chartEditor.playbarSongRemaining = newRemainingLabel;
                    addedKeysComponent = true;
                }
            }

            setChartEditorKeyCount(chartEditor);
        }
    }

    public function setChartEditorKeyCount(chartEditor:ChartEditorState) {
        var currentEvent:ChartEditorEventSprite = null;
        for (eventSprite in chartEditor.renderedEvents) {
            if (FlxG.overlap(chartEditor.gridPlayhead, eventSprite)) {
                currentEvent = eventSprite;
                break;
            }
        }

        if (currentEvent != null && currentEvent.eventData != null && currentEvent.eventData.eventKind == "KeyCountEvent" && currentEvent.eventData.value != null) {
            updateChartEditor(chartEditor, currentEvent.eventData.value.keyCount);
        }
    }

    // Updates the Chart Editor based on a custom Key Count
    function updateChartEditor(chartEditor:ChartEditorState, keyCount:Int) {
        ChartEditorState.STRUMLINE_SIZE = FNFLegacyImporter.STRUMLINE_SIZE = Strumline.KEY_COUNT = keyCount;
        ChartEditorThemeHandler.TOTAL_COLUMN_COUNT = ChartEditorState.STRUMLINE_SIZE * 2 + 1;
        ChartEditorState.NOTE_PREVIEW_X_POS = -(keyCount * ChartEditorState.GRID_SIZE) + 480;
        ChartEditorThemeHandler.updateGridBitmap(chartEditor);
        chartEditor.gridTiledSprite.x = chartEditor.gridPlayhead.x = chartEditor.buttonSelectOpponent.x = FlxG.width / 2 - ChartEditorState.GRID_SIZE * ChartEditorState.STRUMLINE_SIZE;
        chartEditor.notePreview.x = chartEditor.notePreviewPlayhead.x = chartEditor.notePreviewViewport.x = -(keyCount * ChartEditorState.GRID_SIZE) + 480;
        chartEditor.measureTicks.x = chartEditor.gridTiledSprite.x - ChartEditorState.GRID_SIZE;

        chartEditor.gridTiledSprite.width = chartEditor.gridBitmap.width;

        chartEditor.gridPlayhead.remove(chartEditor.gridPlayhead.members[0]);
        var playheadWidth = ChartEditorState.GRID_SIZE * (ChartEditorState.STRUMLINE_SIZE * 2 + 1) + Std.int(ChartEditorState.GRID_SIZE) * 2;
        var playheadSprite = new FunkinSprite().makeSolidColor(playheadWidth, ChartEditorState.PLAYHEAD_HEIGHT, ChartEditorState.PLAYHEAD_COLOR);
        playheadSprite.x = -ChartEditorState.PLAYHEAD_SCROLL_AREA_WIDTH;
        playheadSprite.y = 0;
        chartEditor.gridPlayhead.add(playheadSprite);

        chartEditor.buttonSelectOpponent.width = chartEditor.buttonSelectPlayer.width = ChartEditorState.GRID_SIZE * keyCount;
        chartEditor.buttonSelectPlayer.x = chartEditor.buttonSelectOpponent.x + chartEditor.buttonSelectOpponent.width;
        chartEditor.buttonSelectEvent.x = chartEditor.buttonSelectPlayer.x + chartEditor.buttonSelectPlayer.width;
        chartEditor.buttonSelectPlayer.findComponent(null, Label, false).wordWrap = chartEditor.buttonSelectOpponent.findComponent(null, Label, false).wordWrap = false;

        chartEditor.renderedHoldNotes.clear();
        chartEditor.renderedNotes.clear();
        chartEditor.renderedEvents.clear();
        chartEditor.renderedSelectionSquares.clear();
        chartEditor.buildNoteGroup();
        chartEditor.noteDisplayDirty = true;

        if (chartEditor.audioWaveforms.members[0] != null) chartEditor.audioWaveforms.members[0].x = chartEditor.healthIconDad.x + chartEditor.healthIconDad.width / 2;
        if (chartEditor.audioWaveforms.members[1] != null) chartEditor.audioWaveforms.members[1].x = chartEditor.healthIconBF.x + chartEditor.healthIconBF.width / 2;
    }
}