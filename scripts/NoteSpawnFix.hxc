import funkin.Highscore;
import funkin.Paths;
import funkin.modding.events.ScriptEvent;
import funkin.modding.module.ScriptedModule;
import funkin.play.PlayState;
import funkin.play.notes.Strumline;

class NoteSpawnFix extends ScriptedModule {
    var mkHooks:MultikeyHooks;

    var state:PlayState;

    function new() {
        super("NoteSpawnFix", 1);
        mkHooks = new MultikeyHooks();
    }

    override function onSongLoaded(event:SongLoadScriptEvent) {
        if (PlayState.instance == null && Strumline.KEY_COUNT == 4) return;
        state = PlayState.instance;
    }

    override function onCountdownStart(event:CountdownScriptEvent) {
        if (Strumline.KEY_COUNT != 4) regenNoteData();
    }

    override function onSongEvent(event:SongEventScriptEvent) {
        if (event.eventData.eventKind == "KeyCountEvent") {
            reapplyNoteData();
        }
    }

    override function onNoteIncoming(event:NoteScriptEvent) {
        if (state == null || Strumline.KEY_COUNT == 4) return;

        // Set correct direction for incoming Notes
        event.note.direction = event.note.noteData.data % Strumline.KEY_COUNT;
        event.note.x -= PlayState.instance.playerStrumline.getXPos(event.note.noteData.data % 4);
        event.note.x += Strumline.NOTE_SPACING * event.note.direction;

        if (event.note.get_isHoldNote()) {
            // Set correct color for Sustain Notes
            event.note.holdNoteSprite.loadGraphic(Paths.image("NOTE_hold_assets_" + mkHooks.NOTE_COLORS[mkHooks.MULTIKEY_DIRECTIONS[Strumline.KEY_COUNT][event.note.direction]]));
            event.note.holdNoteSprite.noteDirection = event.note.direction;
            event.note.holdNoteSprite.graphicWidth = event.note.holdNoteSprite.graphic.width / 8 * event.note.holdNoteSprite.zoom;
            event.note.holdNoteSprite.updateColorTransform();
            event.note.holdNoteSprite.updateClipping();
            event.note.holdNoteSprite.updateHitbox();

            event.note.holdNoteSprite.x = event.note.x + (Strumline.STRUMLINE_SIZE / 2) - (event.note.holdNoteSprite.get_width() / 2);
        }
    }

    override function onUpdate(event:UpdateScriptEvent) {
        if (state == null || Strumline.KEY_COUNT == 4) return;
        if (state.needsReset) regenNoteData();
    }

    /**
    * Regens the Note Data with the multikey directions
    */
    function regenNoteData() {
        reapplyNoteData();

        Highscore.tallies.combo = 0;
        Highscore.tallies.totalNotes = 0;
        // Highscore.tallies = new Tallies();
    }

    function reapplyNoteData() {
        cleanStrumline(state.playerStrumline);
        cleanStrumline(state.opponentStrumline);

        // Reset the notes on each strumline.
        var playerNoteData = [];
        var opponentNoteData = [];
        var noteData = state.currentChart.notes;

        for (songNote in noteData) {
            var strumTime:Float = songNote.time;
            if (strumTime < 0) continue; // Skip notes that are before the start time.

            switch (songNote.getStrumlineIndex(Strumline.KEY_COUNT)) {
                case 0: playerNoteData.push(songNote);
                case 1: opponentNoteData.push(songNote);
            }
        }

        state.playerStrumline.applyNoteData(playerNoteData);
        state.opponentStrumline.applyNoteData(opponentNoteData);
    }

    function cleanStrumline(strumline:Strumline) {
        for (note in strumline.notes.members) {
            if (note == null) continue;
            strumline.killNote(note);
        }

        for (holdNote in strumline.holdNotes.members) {
            if (holdNote == null) continue;
            holdNote.kill();
        }

        for (splash in strumline.noteSplashes) {
            if (splash == null) continue;
            splash.kill();
        }

        for (cover in strumline.noteHoldCovers) {
            if (cover == null) continue;
            cover.kill();
        }

        strumline.heldKeys = [false, false, false, false];
    }

    // Cleanup
    override function onSubStateCloseBegin(event:SubStateScriptEvent) { if (Std.isOfType(event.targetState, PlayState)) state = null; }

    override function onSongEnd(event:ScriptEvent) { state = null; }

    override function onDestroy(event:ScriptEvent) { state = null; }
}